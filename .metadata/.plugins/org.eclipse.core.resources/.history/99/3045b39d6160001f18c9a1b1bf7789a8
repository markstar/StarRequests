<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   creationComplete="init()">
	<fx:Script>
		<![CDATA[
			protected var _loader:URLLoader;
			
			protected function init():void
			{				
				_loader = new URLLoader();
				_loader.addEventListener( ProgressEvent.PROGRESS, progressListener );
				_loader.addEventListener( Event.COMPLETE, completeListener );
				_loader.addEventListener( SecurityErrorEvent.SECURITY_ERROR, securityErrorListener );
				_loader.addEventListener( IOErrorEvent.IO_ERROR, ioErrorListener );
				_loader.load( new URLRequest( "http://feeds.feedburner.com/markstarblog?format=xml" ) );
			}
			
			protected function cleanup():void
			{	
				_loader.removeEventListener( ProgressEvent.PROGRESS, progressListener );
				_loader.removeEventListener( Event.COMPLETE, completeListener );
				_loader.removeEventListener( SecurityErrorEvent.SECURITY_ERROR, securityErrorListener );
				_loader.removeEventListener( IOErrorEvent.IO_ERROR, ioErrorListener );
				_loader = null;
			}
			
			protected function progressListener( e:ProgressEvent ):void
			{
				trace( e.bytesLoaded / e.bytesTotal );
			}
			
			protected function completeListener( e:Event ):void
			{
				// try..catch needed to check for valid XML.
				try
				{
					var xml:XML = new XML( e.currentTarget.data );
					trace( xml.toString() );
				}
				catch( e:Error )
				{
					showError( e.message.toString() );
				}
				
				cleanup();
			}
			
			protected function securityErrorListener( e:SecurityErrorEvent ):void
			{
				showError( e.text );
				cleanup();
			}
			
			protected function ioErrorListener( e:IOErrorEvent ):void
			{
				showError( e.text );
				cleanup();
			}
			protected function showError( message:String ):void
			{
				trace( message );
			}
		]]>
	</fx:Script>
</s:Application>
